*:/op [$player] [$flags] [$] = >>>
    _handle_command(array(name: 'op', description: 'Op a player.',
    usage: '/<command> <player> [-s]', rank: 1, source: 0));

    if (!$player)
    {
        die('/<command> <player> [-s]');
    }

    string @p = _get_player($player);
    boolean @silent = false;
    string @flags = '';
    if ($flags)
    {
        @flags = $flags;
    }

    if (_has_flag(@flags, 's'))
    {
        if (!_check_permission(player(), 2))
        {
            msg(color(c).'You do not have permission to silence this command.');
        }
        else
        {
            @silent = true;
        }
    }

    if (!ponline(@p))
    {
        if (!_check_permission(player(), 2))
        {
            die(color(c).'You do not have permission to op offline players.');
        }

        if (!array_contains(_get_players(), @p) || @p[0] == '~')
        {
            die(_util_msg('PLAYER_NOT_FOUND'));
        }

        if (!@silent)
        {
            _admin_msg(player(), 'Opping '.@p);
        }
        _set_op(puuid(@p, true), true);
        die();
    }

    if (!@silent)
    {
        _admin_msg(player(), 'Opping '.@p);
    }
    _set_op(puuid(@p, true), true);
    tmsg(@p, _util_msg('OPPED'));
<<<

